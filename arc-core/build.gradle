buildscript {
    repositories {
        mavenCentral()
        google()
        gradlePluginPortal()
        maven { url "https://jitpack.io" }
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm"
    id "com.badlogicgames.jnigen.jnigen-gradle"
}

sourceSets.main.java.srcDirs = ["src"]
sourceSets.main.kotlin.srcDirs = ["src"]
sourceSets.test.java.srcDirs = ["test"]
sourceSets.test.kotlin.srcDirs = ["test"]
sourceSets.test.resources.srcDirs = ["test/resources"]

configurations{
    extraLibs
}

dependencies{
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    testImplementation libraries.junit
    testImplementation aproj(":natives:natives-desktop")
    testImplementation files("unsafe/unsafe.jar")
    //file generated from UnsafeBuffers.java
    compileOnly files("unsafe/unsafe.jar")
    extraLibs files("unsafe/unsafe.jar")
}

jar{
    from{
        configurations.extraLibs.collect{ it.isDirectory() ? it : zipTree(it) }
    }
}

tasks.register("compileBuffersUnsafe", Exec){
    workingDir "unsafe"
    commandLine "javac --target 8 --source 8 -d . UnsafeBuffers.java".split(" ")
}

tasks.register("compileBuffersSafe", Exec){
    workingDir "unsafe"
    commandLine "javac --target 16 --source 16 -d . Java16Buffers.java".split(" ")
}

tasks.register("recompileUnsafe", Exec){
    dependsOn "compileBuffersUnsafe", "compileBuffersSafe"
    workingDir "unsafe"
    commandLine "jar cvf unsafe.jar arc".split(" ")
    doLast{
        delete{
            delete "unsafe/arc"
        }
    }
}

test{
    testLogging{
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}

file("jni").mkdir()

jnigen{
    sharedLibName = "arc"
    temporaryDir = file("build/target/native").absolutePath
    libsDir = "libs"

    all{
        headerDirs += ["soloud/include"]
        cppIncludes = ["*.cpp", "soloud/src/core/**", "soloud/src/audiosource/wav/**", "soloud/src/filter/**"]
        cIncludes = ["*.c", "soloud/src/core/**", "soloud/src/audiosource/wav/**", "soloud/src/filter/**"]
        // 修改：使用数组列表赋值
        cppFlags += ["-DSOLOUD_MAX_VOICE_COUNT=100"]
    }

    // 以下是根据补丁修改的平台定义语法
    addLinux(x64, x86){
        cppIncludes += ["soloud/src/backend/miniaudio/*.cpp"]
        cppFlags += ["-DWITH_MINIAUDIO"]
        libraries += " -lpthread -lrt -lm -ldl"
    }

    addWindows(x32, x86){
        cppIncludes += ["soloud/src/backend/miniaudio/*.cpp"]
        cppFlags += ["-DWITH_MINIAUDIO"]
    }

    addWindows(x64, x86){
        cppIncludes += ["soloud/src/backend/miniaudio/*.cpp"]
        cppFlags += ["-DWITH_MINIAUDIO"]
    }

    addAndroid(){
        linkerFlags += " -llog -lOpenSLES -Wl,-z,max-page-size=0x4000"
        cppIncludes += ["soloud/src/backend/opensles/*.cpp"]
        cppFlags += ["-DWITH_OPENSLES"]
    }

    addMac(x64, x86){
        cppIncludes += ["soloud/src/backend/coreaudio/*.mm"]
        cppFlags += ["-std=c++11", "-DWITH_COREAUDIO"]
        libraries += "-Wl,-framework,CoreAudio -Wl,-framework,AudioToolbox"
    }

    addMac(x64, ARM){
        cppIncludes += ["soloud/src/backend/coreaudio/*.mm"]
        cppFlags += ["-std=c++11", "-DWITH_COREAUDIO"]
        libraries += "-Wl,-framework,CoreAudio -Wl,-framework,AudioToolbox"
    }

    addIOS(){
        headerDirs += ["iosgl"]
        cppIncludes += ["soloud/src/backend/coreaudio/*.mm", "iosgl/**"]
        cppFlags += ["-stdlib=libc++", "-std=c++11", "-DWITH_COREAUDIO"]
        libraries += "-Wl,-framework,CoreAudio -Wl,-framework,AudioToolbox"
        linkerFlags += " -undefined dynamic_lookup "
    }
}

tasks.register('copyUnsafeStuff'){
    doLast{
        copy{
            from zipTree("unsafe/unsafe.jar")
            into "build/classes/java/main"
            include '**/*.class'
        }
    }
}

getTasksByName("jnigen", true).each{
    it.dependsOn copyUnsafeStuff
}

tasks.register('cleanNatives'){
    doLast{
        delete{ delete "$rootDir/arc-core/jni" }
        delete{ delete "$rootDir/arc-core/libs" }
        delete{ delete "$rootDir/arc-core/csrc/soloud" }
        delete{ delete "$rootDir/arc-core/csrc/stb_image.h" }
    }
}

tasks.register('preJni'){
    doFirst{
        if(!file("csrc/stb_image.h").exists()){
            println "Fetching stb_image source..."
            "curl -o $rootDir/arc-core/csrc/stb_image.h https://raw.githubusercontent.com/nothings/stb/013ac3beddff3dbffafd5177e7972067cd2b5083/stb_image.h".execute().waitFor()
        }

        if(!file("csrc/soloud").exists()){
            println "Fetching soloud source..."
            "git clone --depth 1 --branch $versions.soloud https://github.com/Anuken/soloud.git $rootDir/arc-core/csrc/soloud".execute().waitFor()
        }

        copy{
            from "csrc/"
            into "jni/"
            include "**"
        }
    }
}

tasks.register('postJni'){
    doLast{
        copy{
            from "libs/linux64", "libs/windows64", "libs/macosx64"
            into "${project.rootDir}/natives/natives-desktop/libs"
            include "**"
        }

        ["arm64-v8a", "x86", "x86_64", "armeabi-v7a"].each{ p ->
            copy{
                from "libs/$p"
                into "${project.rootDir}/natives/natives-android/libs/$p/"
                include "**"
            }
        }

        [".tvos", ""].each{ p ->
            copy{
                from "libs/ios32/libarc.a$p"
                into "${project.rootDir}/natives/natives-ios/libs/"
                include "**"
            }
        }
    }
}

jnigenBuildHost.finalizedBy postJni

getTasksByName("jnigen", true).each{
    it.dependsOn preJni
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach{
    kotlinOptions.jvmTarget = "1.8"
}